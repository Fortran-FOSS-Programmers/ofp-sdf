module ofp-simplify

imports
   libstratego-lib
   Main

signature
  constructors

   CommonStmt        : A * B * C -> D
   InitialSpecPart   : A -> B
   SpecificationPart : A -> B
   

   RewindStmt        : OptLabel * List(PositionSpec) * EOS -> RewindStmt
   rewind-unit-stmt  : OptLabel * FileUnitNumber * EOS     -> RewindStmt


   // For ambiguities
   amb : list -> choice


strategies //=================START OF STRATEGIES============================

  io-ofp-simplify =
    io-wrap(ofp-simplify)

  ofp-simplify =
//      outermost(ofp-disamb)
//    ; innermost(ofp-simplify-ast)
      innermost(ofp-simplify-ast)

rules //========================START OF RULES===============================

// Disambiguation rules
//

//R631 : C633 ... AllocateShapeSpecList shall appear
// TODO - remove: solved by adding {avoid} to Allocation with arity 1
//ofp-disamb:  amb([Allocation(u),   Allocation(x,y)  ]) -> Allocation(x,y)
//ofp-disamb:  amb([Allocation(u,v), Allocation(x,y,z)]) -> Allocation(x,y,z)

//R703,R723
//ofp-disamb:  amb([DefinedOperator(x), DefinedOperator(y)]) -> DefinedOperator(x)


// General catch-all rules
//

ofp-simplify-ast:  no-list()                      -> []
//ofp-simplify-ast:  opt-list(list)                 -> list

ofp-simplify-ast:  ImplicitPart(list, stmt)       -> <conc>(list, [stmt])

ofp-simplify-ast:  no-implicit-part()             -> []

//R204
ofp-simplify-ast:  SpecificationPart(l1,l2,l3,l4) -> SpecificationPart(<conc>(l1,l2,l3,l4))
ofp-simplify-ast:  initial-spec-part(l1,l2)       -> InitialSpecPart(<conc>(l1,l2))

//R403
ofp-simplify-ast:  intrinsic-type-spec(type)      -> type

//R503
ofp-simplify-ast:  opt-array-spec(spec)           -> spec
ofp-simplify-ast:  opt-coarray-spec(spec)         -> spec
ofp-simplify-ast:  opt-char-length(length)        -> length

//R508
ofp-simplify-ast:  language-binding-name(str)     -> str

//R560
ofp-simplify-ast:  letter(char)                   -> char

//R568
ofp-simplify-ast:
  common-stmt(label,entry,list,eos)               -> CommonStmt(label, <concat>[[entry], list], eos)

//R611
ofp-simplify-ast:  DataRef([str])                 -> str

//R612
ofp-simplify-ast:
  PartRef(name,no-section-subscripts(),
               no-image-selector())               -> name

//R622
ofp-simplify-ast:  opt-stride(stride)             -> stride

//R624
ofp-simplify-ast:  ImageSelector(list)            -> list  // reduces '[' list ']' to list

//R701
ofp-simplify-ast:  Primary(expr)                  -> expr  // reduces '(' Expr ')' to Expr

//ofp-simplify-ast:  DataRef([PartRef(obj,_,_)])    -> obj

//R924
ofp-simplify-ast:  backspace-unit-stmt(label,u,eos) -> BackspaceStmt(label,[UNIT(u)],eos)

//R925
ofp-simplify-ast:  endfile-unit-stmt(label,u,eos)   -> EndfileStmt(label,[UNIT(u)],eos)

//R926
ofp-simplify-ast:  rewind-unit-stmt(label,u,eos)    -> RewindStmt(label,[UNIT(u)],eos)

//R928
ofp-simplify-ast:  flush-unit-stmt(label,u,eos)     -> FlushStmt(label,[UNIT(u)],eos)

//R1003
ofp-simplify-ast:  format-items-first(item)         -> [item]
ofp-simplify-ast:  format-items(list, item)         -> <concat>[list, [item]]

//R1109
//ofp-simplify-ast:  UseStmt(label,nature,name,opt-rename-list(list),eos)
//                      -> UseStmt(label,nature,name,list,OnlyList([]),eos)
//ofp-simplify-ast:  UseStmt(label,nature,name,opt-only-list(list),eos)
//                      -> UseStmt(label,nature,name,RenameList([]),list,eos)

ofp-simplify-ast:  opt-module-nature(nature)      -> nature
