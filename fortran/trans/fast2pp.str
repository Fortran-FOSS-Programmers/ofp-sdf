module fast2pp

imports
   libstratego-lib
   AST

signature
  constructors

    PpMainProgram  : OfpMainProgram  -> PpMainProgram
    PpModule       : OfpModule       -> PpModule
    PpFunction     : OfpFunction     -> PpFunction
    PpSubroutine   : OfpSubroutine   -> PpSubroutine
    PpMpSubprogram : OfpMpSubprogram -> PpMpSubprogram

    PpEntityDecl   : name * PpArraySpec * PpCoarraySpec * CharLength * Initialization -> PpEntityDecl
    PpArraySpec    : ArraySpec       -> PpArraySpec
    PpCoarraySpec  : CoarraySpec     -> PpCoarraySpec

    PpActualArgSpec: PpKeyword * arg -> PpActualArgSpec
    PpKeyword      : keyword         -> PpKeyword

    PpDimSpec      : upper            -> PpDimSpec
    PpDimSpec      : lower * upper    -> PpDimSpec

    PpLetterSpec   : Spec            -> PpLetterSpec
    PpLetterSpec   : Spec * Spec     -> PpLetterSpec

    PpPartRef           : name * ss * is -> PpPartRef
    PpSectionSubscripts : ss             -> PpSectionSubscripts
    PpImageSelector     : ss             -> PpImageSelector

    eos            : EOStmt          -> EOS

    PpSelectCaseStmt : OptLabel * CaseConstructName * CaseExpr * EOS -> PpSelectCaseStmt    // R838

    PpNonlabelDoStmt : OptLabel * DoConstructName * OptLoopControl * EOS -> PpNonlabelDoStmt  //R817

    PpAssociateStmt  : OptLabel * NoName * List(Association) * EOS -> PpAssociateStmt        //R803

//    PpSelectTypeStmt : OptLabel * NoSelectConstructName * OptAssociateName * Selector * EOS -> PpSelectTypeStmt  //R847

    opt-list       : list            -> opt-list
    append-colon   : name            -> append-colon


strategies

  main =
    io-wrap(fast2pp)

  fast2pp =
      outermost(ast-to-pp)
    ; outermost(no-to-none)

  pp-opt-list =
      where(<gt>(<length>,0))
    ; !opt-list(<id>)

  append-colon = ?no-name() < id + !append-colon(<id>)

rules

// Don't print EOS and associated comments
//
  ast-to-pp:  eos(_)                       -> None()

//R503
  ast-to-pp:
    EntityDecl(name,as,cs,cl,init)         -> PpEntityDecl(name,PpArraySpec(as),PpCoarraySpec(cs),cl,init)

//R510
  ast-to-pp:
    DimSpec(no-lower-bound(), upper)       -> PpDimSpec(upper)
  ast-to-pp:
    DimSpec(lower, upper)                  -> PpDimSpec(lower, upper)

//R562
  ast-to-pp:  LetterSpec(s1,no-letter())   -> PpLetterSpec(s1)
  ast-to-pp:  LetterSpec(s1,s2)            -> PpLetterSpec(s1,s2)

//R612
  ast-to-pp:
    PartRef(name,ss,is) -> PpPartRef(name,PpSectionSubscripts(ss),PpImageSelector(is))

//R803
  ast-to-pp:
    AssociateStmt(label,name,list,eos) -> PpAssociateStmt(label,<append-colon>name,list,eos)

//R817
  ast-to-pp:
    NonlabelDoStmt(label,name,loopcontrol,eos) -> PpNonlabelDoStmt(label,<append-colon>name,loopcontrol,eos)

//R838
  ast-to-pp:
    SelectCaseStmt(label,name,expr,eos)    -> PpSelectCaseStmt(label,<append-colon>name,expr,eos)

//R847
//  ast-to-pp:
//    SelectTypeStmt(label,name,name,selector,eos) -> PpSelectTypeStmt(label,<append-colon>name,name,selector,eos)

//R860
//  ast-to-pp:
//    SyncImagesStmt(label,set,list,eos)     -> SyncImagesStmt(label,set,<pp-opt-list>(list))

//R1101
  ast-to-pp:  OfpMainProgram(name, scope)  -> PpMainProgram(scope)

//R1104
  ast-to-pp:  OfpModule(name, scope)       -> PpModule(scope)

//R1222
  ast-to-pp:  ActualArgSpec(keyword, arg)  -> PpActualArgSpec(PpKeyword(keyword), arg)

//R1227
  ast-to-pp:  OfpFunction(name, scope)     -> PpFunction(scope)

//R1233
  ast-to-pp:  OfpSubroutine(name, scope)   -> PpSubroutine(scope)

//R1237
  ast-to-pp:  OfpMpSubprogram(name, scope) -> PpMpSubprogram(scope)


  no-to-none: no-label()                   -> None()   // R312
  no-to-none: no-program-name()            -> None()   // R1103
  no-to-none: no-program-stmt()            -> None()   // R1102
  no-to-none: no-kind-param()              -> None()   // R408
  no-to-none: no-char-selector()           -> None()   // R420
  no-to-none: no-char-length()             -> None()   // R503
  no-to-none: no-init()                    -> None()   // R503
  no-to-none: no-kind()                    -> None()   // R405
  no-to-none: no-prefix()                  -> None()   // R1225
  no-to-none: no-language-binding()        -> None()   // R1229
  no-to-none: no-generic-spec()            -> None()   // R1204
  no-to-none: no-module-name()             -> None()   // R1106
  no-to-none: no-module-nature()           -> None()   // R1109
  no-to-none: no-block-data-name()         -> None()   // R1122
  no-to-none: no-proc-interface()          -> None()   // R1212
  no-to-none: no-subroutine-name()         -> None()   // R1236
  no-to-none: no-binding-name()            -> None()   // R508
  no-to-none: no-expr()                    -> None()   // R1241
  no-to-none: no-stop-code()               -> None()   // R857
  no-to-none: no-r()                       -> None()   // R1004
  no-to-none: no-m()                       -> None()   // R1007
  no-to-none: no-e()                       -> None()   // R1007
  no-to-none: no-name()                    -> None()   // R839, R850
  no-to-none: no-parent-submodule-name()   -> None()   // R1118
  no-to-none: no-high()                    -> None()   // R844
  no-to-none: no-low()                     -> None()   // R844
  no-to-none: no-stride()                  -> None()   // R920
  no-to-none: no-type-spec()               -> None()   // R626
  no-to-none: no-lower-bound()             -> None()   // R633
  no-to-none: no-component-name()          -> None()   // R734
  no-to-none: no-upper-bounds()            -> None()   // R736
  no-to-none: no-type-bound-procedure-part()  -> None()   //R445
  no-to-none: no-binding-private-stmt()    -> None()   // R446
  no-to-none: no-arg-name()                -> None()   // R451
  no-to-none: no-subprogram-part()         -> None()   // R210
  no-to-none: no-repeat()                  -> None()   // R541
  no-to-none: no-forall-step()             -> None()   // R753
  no-to-none: no-mask-expr()               -> None()   // R752
  no-to-none: no-else-stmt()               -> None()   // R832
  no-to-none: no-loop-control()            -> None()   // R818
  no-to-none: no-subscript()               -> None()   // R619

  no-to-none: PpKeyword(no-keyword())      -> None()   // R215

  no-to-none: PpArraySpec([])              -> None()   // R515
  no-to-none: PpCoarraySpec([])            -> None()   // R509

  no-to-none: PpSectionSubscripts(no-section-subscripts()) -> None()
  no-to-none: PpImageSelector(no-image-selector())         -> None()